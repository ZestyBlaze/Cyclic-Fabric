plugins {
	id 'fabric-loom' version '1.4-SNAPSHOT'
	id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
	maven { url "https://maven.parchmentmc.org" } // Parchment
	maven { url "https://maven.ladysnake.org/releases" } // Cardinal Components, PAL
	maven { url "https://raw.githubusercontent.com/Fuzss/modresources/main/maven/" } // Forge Config API Port
	maven { url "https://maven.jamieswhiteshirt.com/libs-release" } // Reach Entity Attributes
	maven { url "https://jitpack.io" } // ZestyLib
}

dependencies {
	minecraft("com.mojang:minecraft:${project.minecraft_version}")
	mappings loom.layered() {
		officialMojangMappings()
		parchment("org.parchmentmc.data:parchment-${project.minecraft_version}:${project.parchment_version}@zip")
	}
	modImplementation("net.fabricmc:fabric-loader:${project.loader_version}")
	modImplementation("net.fabricmc.fabric-api:fabric-api:${project.fabric_version}")

	modApi("fuzs.forgeconfigapiport:forgeconfigapiport-fabric:${project.config_version}")

	modApi(include("teamreborn:energy:${project.energy_version}"))

	modImplementation(include("dev.onyxstudios.cardinal-components-api:cardinal-components-base:${project.cc_version}"))
	modImplementation(include("dev.onyxstudios.cardinal-components-api:cardinal-components-entity:${project.cc_version}"))

	modImplementation(include("io.github.ladysnake:PlayerAbilityLib:${pal_version}"))

	modImplementation include("com.github.ZestyBlaze:ZestyLib:${project.zlib_version}")
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

java {
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
}

loom {
	accessWidenerPath = file("src/main/resources/cyclic.accessWidener")
}

configurations.all {
	resolutionStrategy {
		force("net.fabricmc:fabric-loader:${project.loader_version}")
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	repositories {
	}
}